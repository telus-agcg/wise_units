// <main-term>
main_term = ${ SOI ~ (slash ~ term | term) ~ EOI }

// <term>
term = {
    component ~ dot ~ term | component ~ slash ~ term | component
}

dot = { "." }
slash = { "/" }

// <component>
component = { factor ~ basic_component | basic_component }

basic_component = {
    annotatable ~ annotation_group |
        annotatable |
        annotation_group |
        factor |
        "(" ~ term ~ ")"
}

// <annotation>
annotation_group = _{ "{" ~ annotation ~ "}" }
annotation = { annotation_char+ }

// <annotatable>
annotatable = { simple_unit ~ exponent | simple_unit }

// <simple-unit>
simple_unit = {
    "10^"
        | "10*"
        | symbol_char+
}

symbol_char = _{
    "!" // 33
        | '#'..'\'' // 35-39
        | "*"       // 42
        | ","       // 44
        | ':'..'<'  // 58-60
        | '>'..'z'  // 62-90
        | "^"       // 94
        | "|"       // 124
        | "~"       // 126
}

annotation_char = _{
    '!'..'z'  // 33-122
        | "|" // 124
        | "~" // 126
}

exponent = { sign? ~ digits }
factor = { ASCII_DIGIT+ ~ !not_factor  }
not_factor = _{ "*" | "^" }
digits = { ASCII_DIGIT+ }
sign = { "-" | "+" }

// vim:ft=rust
